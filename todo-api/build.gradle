plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.5'
	id 'io.spring.dependency-management' version '1.1.6'
	id "org.openapi.generator" version "7.8.0"
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'io.swagger.core.v3:swagger-annotations:2.2.8'
	implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.3'
	implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.h2database:h2'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:3.0.3'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

// APIドキュメントを自動生成するためのタスク
task buildApiDoc(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
	generatorName.set("html2")
	inputSpec.set("$rootDir/src/main/resources/schema-api.yaml")
	outputDir.set("$buildDir/apidoc")
}

//スキーマからSpringコードを自動生成するためのタスク
task buildSpringServer(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
	generatorName.set("spring")
	inputSpec.set("$rootDir/src/main/resources/schema-api.yaml")
	outputDir.set("$buildDir/spring")
	apiPackage.set("com.example.todoapi.controller")
	modelPackage.set("com.example.todoapi.model")
	configOptions.set([
			interfaceOnly: "true",
			useSpringBoot3: "true"
	])
	globalProperties.set([
			modelDocs: "false"
	])
}

// Javaコンパイル時、Springコード自動生成タスクを実行する
compileJava.dependsOn tasks.buildSpringServer

// 生成したSpringコード(クラス)を自動importする（import文）
sourceSets.main.java.srcDir "$buildDir/spring/src/main/java"